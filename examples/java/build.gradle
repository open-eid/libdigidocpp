plugins {
    id 'java'
}
group = 'ee.ria'
java {
    targetCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
}
base {
    archivesName = 'libdigidocpp'
}
jar {
    manifest {
        attributes 'Main-Class': 'ee.ria.libdigidocpp.libdigidocpp'
    }
}
def digidocppJarPath = '/usr/share/digidocpp/digidocpp.jar'
def nativeLibDir = null
if (System.properties['os.name'].toLowerCase().contains('windows')) {
    digidocppJarPath = 'C:\\Program Files\\libdigidocpp\\digidocpp.jar'
    nativeLibDir = 'C:\\Program Files\\libdigidocpp'
} else if (System.properties['os.name'].toLowerCase().contains('mac')) {
    digidocppJarPath = '/Library/Java/Extensions/digidocpp.jar'
}
dependencies {
    implementation files(digidocppJarPath)
}
tasks.withType(JavaExec).configureEach {
    if (nativeLibDir != null) {
        environment 'PATH', "${nativeLibDir};" + System.getenv('PATH')
        jvmArgs "-Djava.library.path=${nativeLibDir}"
    }
}