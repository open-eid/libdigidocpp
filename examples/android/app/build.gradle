apply plugin: 'com.android.application'

def libsPath = '/Library/libdigidocpp'

android {
    compileSdk 32
    defaultConfig {
        applicationId "ee.ria.libdigidocpp"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"
        if (System.getenv("BUILD_NUMBER")) {
            versionCode Integer.parseInt(System.getenv("BUILD_NUMBER"))
            versionName "1.0." + System.getenv("BUILD_NUMBER")
        }
        setProperty("archivesBaseName", "libdigidocpp-android-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets.main.java.srcDirs += [libsPath + '.androidarm/include']
}

dependencies {
    implementation 'androidx.core:core:1.7.0'
}

task schemaZip(type:Zip) {
    println "Create schema zip"
    from (libsPath + '.androidarm/etc/digidocpp/schema/') {
        include '*'
    }
    destinationDirectory = file('src/main/res/raw/')
    archiveFileName = 'schema.zip'
}

task copyLibs {
    println "Copy jniLibs"
    copy {
        from libsPath + '.androidarm/lib/libdigidoc_java.so'
        into 'src/main/jniLibs/armeabi-v7a'
    }
    copy {
        from libsPath + '.androidarm64/lib/libdigidoc_java.so'
        into 'src/main/jniLibs/arm64-v8a'
    }
    copy {
        from libsPath + '.androidx86/lib/libdigidoc_java.so'
        into 'src/main/jniLibs/x86'
    }
    copy {
        from libsPath + '.androidx86_64/lib/libdigidoc_java.so'
        into 'src/main/jniLibs/x86_64'
    }
}

preBuild.dependsOn schemaZip, copyLibs
